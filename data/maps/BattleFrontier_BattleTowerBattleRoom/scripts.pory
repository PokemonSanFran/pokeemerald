const OPPONENT = 1
const ATTENDANT_1 = 2
const ATTENDANT_2 = 3

const VAR_BRAIN_STATUS = VAR_TEMP_F

mapscripts BattleFrontier_BattleTowerBattleRoom_MapScripts{
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_1, 0: BattleFrontier_BattleTowerBattleRoom_EventScript_SetUpObjects
    ]
    MAP_SCRIPT_ON_FRAME_TABLE[
        VAR_TEMP_0, 0: BattleFrontier_BattleTowerBattleRoom_EventScript_EnterRoom
    ]
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_SetUpObjects{
    setvar(VAR_TEMP_1, 1)
    applymovement(ATTENDANT_2, BattleFrontier_BattleTowerBattleRoom_Movement_SetInvisible)
    end
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_EnterRoom{
    setvar(VAR_TEMP_0, 1)
    applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerBattleRoom_Movement_PlayerEnter)
    waitmovement(0)

    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    if(var(VAR_RESULT) == 0){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_OpponentEnter)
    }

    applymovement(ATTENDANT_1, BattleFrontier_BattleTowerBattleRoom_Movement_AttendantApproachPlayer)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerBattleRoom_Movement_PlayerFaceAttendant)
    waitmovement(0)

    setvar(VAR_TEMP_2, 1)
    frontier_set(FRONTIER_DATA_RECORD_DISABLED, TRUE)
    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_OpponentEnter{
    tower_setopponent

    addobject(OPPONENT)
    applymovement(OPPONENT, BattleFrontier_BattleTowerBattleRoom_Movement_OpponentEnter)
    waitmovement(0)

    tower_getopponentintro(0)
    msgbox(gStringVar4, MSGBOX_DEFAULT)
    waitmessage

    call(BattleFrontier_BattleTowerBattleRoom_EventScript_DoTowerBattle)
    if(var(VAR_RESULT) == B_OUTCOME_WON){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_DefeatedOpponent)
    }
    goto(BattleFrontier_BattleTower_EventScript_WarpToLobbyLost)
}

script BattleFrontier_BattleTower_EventScript_SetChallengeStatus{
    tower_set(TOWER_DATA_LVL_MODE)
    setvar(VAR_0x8004, FANCOUNTER_USED_BATTLE_TOWER)
    special(Script_TryGainNewFanFromCounter)
    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobby)
}

script BattleFrontier_BattleTower_EventScript_WarpToLobbyLost{
    frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_LOST)
    goto(BattleFrontier_BattleTower_EventScript_SetChallengeStatus)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobbyWon{
    frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_WON)
    goto(BattleFrontier_BattleTower_EventScript_SetChallengeStatus)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_DefeatedOpponent{
    call(BattleFrontier_EventScript_IncrementWinStreak)

    tower_setbattlewon
    if(var(VAR_RESULT) == 7){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobbyWon)
    }

    applymovement(OPPONENT, BattleFrontier_BattleTowerBattleRoom_Movement_OpponentExit)
    waitmovement(0)
    removeobject(OPPONENT)

    frontier_getbrainstatus
    if(var(VAR_RESULT) != FRONTIER_BRAIN_NOT_READY){
        call(BattleFrontier_BattleTowerBattleRoom_EventScript_SecondAttendantEnter)
    }

    applymovement(ATTENDANT_1, BattleFrontier_BattleTowerBattleRoom_Movement_AttendantApproachPlayer)
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerBattleRoom_Movement_PlayerFaceAttendant)
    waitmovement(0)

    msgbox(BattleFrontier_BattleTowerBattleRoom_Text_RestoreMonsToFullHealth, MSGBOX_DEFAULT)
    playfanfare(MUS_HEAL)
    waitfanfare
    special(HealPlayerParty)

    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
}
script BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent{
    frontier_getbrainstatus
    copyvar(VAR_BRAIN_STATUS, VAR_RESULT)
    if(var(VAR_BRAIN_STATUS) != FRONTIER_BRAIN_NOT_READY){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_MaidenUpNext)
    }

    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    call(BattleFrontier_BattleTowerBattleRoom_EventScript_ReadyForOpponent)

    call(BattleFrontier_EventScript_GetCantRecordBattle)
    if (var(VAR_RESULT) == TRUE){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponentNoRecord)
    }

    multichoice(19, 4, MULTI_GO_ON_RECORD_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT)){
        case 0:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_ContinueChallenge)
        case 1:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskRecordBattle)
        case 2:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskPauseChallenge)
        case 3:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
    }
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponentNoRecord{
    multichoice(20, 6, MULTI_GO_ON_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT)){
        case 0:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_ContinueChallenge)
        case 1:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskPauseChallenge)
        case 2:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
    }
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AskRecordBattle{
    message(BattleFrontier_BattleTowerBattleRoom_Text_RecordYourBattle)
    waitmessage
    multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
    if (var(VAR_RESULT) == 0){
        call(BattleFrontier_EventScript_SaveBattle)
    }
    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AskPauseChallenge{
    msgbox(BattleFrontier_BattleTowerBattleRoom_Text_SaveAndQuitGame, MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_PauseChallenge)
    }

    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AskRetireChallenge{
    message(BattleFrontier_BattleTowerBattleRoom_Text_CancelYourChallenge)
    waitmessage
    multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
    if (var(VAR_RESULT) == 0){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_RetireChallenge)
    }

    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForOpponent)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_ContinueChallenge{
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerBattleRoom_Movement_PlayerFaceBattle)
    waitmovement(0)
    applymovement(ATTENDANT_1, BattleFrontier_BattleTowerBattleRoom_Movement_AttendantReturnToPos)
    waitmovement(0)

    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_OpponentEnter)
}


script BattleFrontier_BattleTowerBattleRoom_EventScript_PauseChallenge{
    message(BattleFrontier_BattleTowerBattleRoom_Text_SavingPleaseWait)
    waitmessage

    tower_save(CHALLENGE_STATUS_PAUSED)
    playse(SE_SAVE)
    waitse

    fadescreen(FADE_TO_BLACK)
    frontier_reset
    end
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_ReadyForOpponent{
    copyvar(VAR_TEMP_E, VAR_RESULT)
    addvar(VAR_TEMP_E,1)
    buffernumberstring(STR_VAR_3,VAR_TEMP_E)

    message(BattleFrontier_BattleTowerBattleRoom_Text_ReadyForXOpponent)
    waitmessage

    return
}

script BattleFrontier_EventScript_IncrementWinStreak{
    frontier_incrementstreak
    return
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_SecondAttendantEnter{
    applymovement(ATTENDANT_2, BattleFrontier_BattleTowerBattleRoom_Movement_SecondAttendantEnter)
    waitmovement(0)
    applymovement(ATTENDANT_2, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)

    playse(SE_PIN)

    applymovement(ATTENDANT_1, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(ATTENDANT_1, Common_Movement_Delay48)
    waitmovement(0)

    applymovement(ATTENDANT_2, BattleFrontier_BattleTowerBattleRoom_Movement_SecondAttendantDelay)
    applymovement(ATTENDANT_1, BattleFrontier_BattleTowerBattleRoom_Movement_AttendantFaceSecondAttendant)
    waitmovement(0)
    applymovement(ATTENDANT_2, BattleFrontier_BattleTowerBattleRoom_Movement_SecondAttendantExit)
    waitmovement(0)

    return
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_MaidenUpNext{
    if (var(VAR_TEMP_2) == 1){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaiden)
    }

    msgbox(BattleFrontier_BattleTowerBattleRoom_Text_SalonMaidenOnHerWay, MSGBOX_DEFAULT)
    setvar(VAR_TEMP_2, 1)

    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaiden)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaiden{
    message(BattleFrontier_BattleTowerBattleRoom_Text_ReadyForSalonMaiden)
    waitmessage

    call(BattleFrontier_EventScript_GetCantRecordBattle)
    if (var(VAR_RESULT) == TRUE){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaidenNoRecord)
    }

    multichoice(19, 4, MULTI_GO_ON_RECORD_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT)){
        case 0: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabel)
        case 1: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskRecordBattle)
        case 2: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskPauseChallenge)
        case 3: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaiden)
    }
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaidenNoRecord{
    multichoice(20, 6, MULTI_GO_ON_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT)){
        case 0: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabel)
        case 1: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskPauseChallenge)
        case 2: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AskReadyForMaiden)
    }
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabel{
    call(BattleFrontier_EventScript_SetBrainObjectGfx)
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerBattleRoom_Movement_PlayerFaceBattle)
    waitmovement(0)
    applymovement(ATTENDANT_1, BattleFrontier_BattleTowerBattleRoom_Movement_AttendantReturnToPos)
    waitmovement(0)
    addobject(OPPONENT)
    applymovement(OPPONENT, BattleFrontier_BattleTowerBattleRoom_Movement_AnabelEnter)
    waitmovement(0)

    switch(var(VAR_BRAIN_STATUS)){
        case FRONTIER_BRAIN_GOLD: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_AnabelGoldIntro)
        case FRONTIER_BRAIN_STREAK: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelSilver)
        case FRONTIER_BRAIN_STREAK_LONG: goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelGold)
    }

    frontier_get(FRONTIER_DATA_HEARD_BRAIN_SPEECH)
    if(var(VAR_RESULT) == FALSE){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelSilver)
    }

    msgbox(BattleFrontier_BattleTowerBattleRoom_Text_GreetingsImAnabel, MSGBOX_DEFAULT)
    frontier_set(FRONTIER_DATA_HEARD_BRAIN_SPEECH)

    goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelSilver)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelSilver{
    msgbox(BattleFrontier_BattleTowerBattleRoom_Text_LetMeSeeYourTalent, MSGBOX_DEFAULT)

    call(BattleFrontier_BattleTowerBattleRoom_EventScript_DoTowerBattle)
    if (var(VAR_RESULT) == B_OUTCOME_WON){
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_DefeatedAnabelSilver)
    }

    goto(BattleFrontier_BattleTower_EventScript_WarpToLobbyLost)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_DefeatedAnabelSilver{
    call(BattleFrontier_EventScript_IncrementWinStreak)
    frontier_getsymbols
    if(var(VAR_RESULT) == 0){
        	goto(BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobbyWon)
        }
        msgbox(BattleFrontier_BattleTowerBattleRoom_Text_AnabelTalentShallBeRecognized, MSGBOX_DEFAULT)
        playfanfare(MUS_OBTAIN_SYMBOL)
        message(BattleFrontier_BattleTowerBattleRoom_Text_ReceivedAbilitySymbol)
        waitmessage
        waitfanfare
        frontier_givesymbol
        msgbox(BattleFrontier_BattleTowerBattleRoom_Text_UntilNextTime, MSGBOX_DEFAULT)
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobbyWon)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_AnabelGoldIntro{
    frontier_get(FRONTIER_DATA_HEARD_BRAIN_SPEECH)
        if(var(VAR_RESULT) == FALSE){
        	goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelGold)
        }
        msgbox(BattleFrontier_BattleTowerBattleRoom_Text_AnabelYouCameBack, MSGBOX_DEFAULT)
        frontier_set(FRONTIER_DATA_HEARD_BRAIN_SPEECH)
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelGold)
}
script BattleFrontier_BattleTowerBattleRoom_EventScript_BattleAnabelGold{
    msgbox(BattleFrontier_BattleTowerBattleRoom_Text_LetsBeginShallWe, MSGBOX_DEFAULT)
        call(BattleFrontier_BattleTowerBattleRoom_EventScript_DoTowerBattle)
        if (var(VAR_RESULT) == B_OUTCOME_WON){
            goto(BattleFrontier_BattleTowerBattleRoom_EventScript_DefeatedAnabelGold)
        }
        goto(BattleFrontier_BattleTower_EventScript_WarpToLobbyLost)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_DefeatedAnabelGold{
    call(BattleFrontier_EventScript_IncrementWinStreak)
        frontier_getsymbols
        if (var(VAR_RESULT) == 2){
            goto(BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobbyWon)
        }
        msgbox(BattleFrontier_BattleTowerBattleRoom_Text_AnabelCongratsYourPassPlease, MSGBOX_DEFAULT)
        playfanfare(MUS_OBTAIN_SYMBOL)
        message(BattleFrontier_BattleTowerBattleRoom_Text_AbilitySymbolTookGoldenShine)
        waitmessage
        waitfanfare
        frontier_givesymbol
        msgbox(BattleFrontier_BattleTowerBattleRoom_Text_WishICouldBattleYouAgain, MSGBOX_DEFAULT)
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobbyWon)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_DoTowerBattle{
    closemessage
        setvar(VAR_TEMP_2, 0)
        frontier_set(FRONTIER_DATA_RECORD_DISABLED, FALSE)
        special(HealPlayerParty)
        setvar(VAR_0x8004, SPECIAL_BATTLE_TOWER)
        setvar(VAR_0x8005, 0)
        special(DoSpecialTrainerBattle)
        waitstate
        copyvar(VAR_0x8004, VAR_FRONTIER_BATTLE_MODE)
        if (var(VAR_0x8004) == FRONTIER_MODE_LINK_MULTIS){
            goto(BattleFrontier_BattleTowerBattleRoom_EventScript_EndTowerBattle)
        }
        frontier_restorehelditems
        special(HealPlayerParty)
        frontier_resetsketch
        goto(BattleFrontier_BattleTowerBattleRoom_EventScript_EndTowerBattle)
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_EndTowerBattle{
    tower_setinterviewdata
    frontier_get(FRONTIER_DATA_BATTLE_OUTCOME)
    return
}

script BattleFrontier_EventScript_SetBrainObjectGfx{
    frontier_setbrainobj
    return
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_PlayerEnter{
    walk_up * 3
        face_right
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_PlayerFaceAttendant{
    face_down
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_PlayerFaceBattle{
    face_right
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_OpponentEnter{
    walk_down * 4
        face_left
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_OpponentExit{
    walk_up * 4
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_AttendantApproachPlayer{
    walk_right * 3
        walk_up
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_AttendantReturnToPos{
    walk_down
        walk_left * 3
        face_right
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_SetInvisible{
    set_invisible
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_SecondAttendantEnter{
    set_visible
        delay_16
        walk_up
        walk_left * 2
        delay_8
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_SecondAttendantExit{
    walk_right * 2
        walk_down
        set_invisible
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_SecondAttendantDelay{
    delay_16 * 4
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_AttendantFaceSecondAttendant{
    face_right
        delay_16 * 4
        walk_in_place_right
        step_end
}

movement BattleFrontier_BattleTowerBattleRoom_Movement_AnabelEnter{
    walk_slow_down * 4
        face_left
        step_end
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_WarpToLobby{
    copyvar(VAR_RESULT, VAR_FRONTIER_BATTLE_MODE)
    switch(var(VAR_RESULT)){
        case FRONTIER_MODE_SINGLES:
            warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_LOBBY, 6, 6)
        case FRONTIER_MODE_DOUBLES:
            warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_LOBBY, 10, 6)
        case FRONTIER_MODE_MULTIS:
            warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_LOBBY, 14, 6)
        case FRONTIER_MODE_LINK_MULTIS:
            tower_closelink
            warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_LOBBY, 18, 6)
    }
    waitstate
    end
}

script BattleFrontier_BattleTowerBattleRoom_EventScript_RetireChallenge{
    setflag(FLAG_CANCEL_BATTLE_ROOM_CHALLENGE)
        goto(BattleFrontier_BattleTower_EventScript_WarpToLobbyLost)
        end
}

text BattleFrontier_BattleTowerBattleRoom_Text_RestoreMonsToFullHealth{
	"We will restore your POKéMON to\nfull health."
}

text BattleFrontier_BattleTowerBattleRoom_Text_ReadyForXOpponent{
    "You will be facing opponent no. {STR_VAR_3}.\nAre you ready?"
}

text BattleFrontier_BattleTowerBattleRoom_Text_RecordYourBattle{
    "Record your battle on your\nFRONTIER PASS?"
}

text BattleFrontier_BattleTowerLobby_Text_BattleRecordedOnPass{
    "{PLAYER}'s battle was recorded\non the FRONTIER PASS."
}

text BattleFrontier_BattleTowerBattleRoom_Text_SaveAndQuitGame{
    "Would you like to save and\nquit the game?"
}

text BattleFrontier_BattleTowerBattleRoom_Text_SavingPleaseWait{
    "Saving your battle data.\nPlease wait."
}

text BattleFrontier_BattleTowerBattleRoom_Text_CancelYourChallenge{
    "Would you like to cancel your BATTLE\nROOM challenge?"
}

text BattleFrontier_BattleTowerBattleRoom_Text_RecordCouldntBeSaved{
    "There was an error of some sort.\nYour record could not be saved."
}

text BattleFrontier_BattleTowerBattleRoom_Text_SalonMaidenOnHerWay{
    "Excuse me, but…\pOur leader, the SALON MAIDEN, is on\nher way here in hopes of battling you.\pShe should be arriving very shortly."
}

text BattleFrontier_BattleTowerBattleRoom_Text_ReadyForSalonMaiden{
    "You will be facing the SALON MAIDEN.\nAre you prepared?"
}

text BattleFrontier_BattleTowerBattleRoom_Text_GreetingsImAnabel{
    "Greetings…\nMy name is ANABEL.\pI am the SALON MAIDEN, and I am in\ncharge of running the BATTLE TOWER…\pI have heard several rumors\nabout you…\pIn all honesty, what I have heard does\nnot seem attractive in any way…\pThe reason I've come to see you…\nWell, there is but one reason…"
}

text BattleFrontier_BattleTowerBattleRoom_Text_LetMeSeeYourTalent{
    "Let me see your talent in\nits entirety…"
}

text BattleFrontier_BattleTowerBattleRoom_Text_AnabelTalentShallBeRecognized{
    "ANABEL: Fufufu, nicely done…\pYour FRONTIER PASS, please…\nYour talent shall be recognized."
}

text BattleFrontier_BattleTowerBattleRoom_Text_ReceivedAbilitySymbol{
    "The Ability Symbol was embossed on\nthe FRONTIER PASS!"
}

text BattleFrontier_BattleTowerBattleRoom_Text_UntilNextTime{
    "… … … … … …\pYou have confidence in your POKéMON\nbattling talent, don't you?\pI urge you to keep battling and\nkeep on winning.\pI will be waiting for you.\nUntil the next time we meet…"
}

text BattleFrontier_BattleTowerBattleRoom_Text_AnabelYouCameBack{
    "ANABEL: You really did come back to\nsee me…\p… … … … … …\pYou've won straight matches to see me…\nI won't have to hold back against you…\pIt's been too long now…\pToo long since I've been able to battle\nwithout thinking about anything…"
}

text BattleFrontier_BattleTowerBattleRoom_Text_LetsBeginShallWe{
    "Let's begin, shall we?"
}

text BattleFrontier_BattleTowerBattleRoom_Text_AnabelCongratsYourPassPlease{
    "ANABEL: Fufu, congratulations…\nYour FRONTIER PASS, please…"
}

text BattleFrontier_BattleTowerBattleRoom_Text_AbilitySymbolTookGoldenShine{
    "The Ability Symbol took on\na golden shine!"
}

text BattleFrontier_BattleTowerBattleRoom_Text_WishICouldBattleYouAgain{
    "That was fun…\pI have never had a POKéMON battle\nso enjoyable before…\pI wish I could battle with you again…"
}
