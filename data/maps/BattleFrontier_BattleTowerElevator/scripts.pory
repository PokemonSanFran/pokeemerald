const ATTENDANT = 1

mapscripts BattleFrontier_BattleTowerElevator_MapScripts{
    MAP_SCRIPT_ON_FRAME_TABLE[
	    VAR_TEMP_0, 0: BattleFrontier_BattleTowerElevator_EventScript_EnterElevator
    ]
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE[
        VAR_TEMP_1, 0: BattleFrontier_BattleTowerElevator_EventScript_TurnPlayerNorth
    ]
}

script BattleFrontier_BattleTowerElevator_EventScript_TurnPlayerNorth{
	setvar(VAR_TEMP_1, 1)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	end
}

script BattleFrontier_BattleTowerElevator_EventScript_EnterElevator{
	setvar(VAR_TEMP_0, 1)
	applymovement(ATTENDANT, BattleFrontier_BattleTowerElevator_Movement_AttendantEnter)
	applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerElevator_Movement_PlayerEnter)
	waitmovement(0)
	special(BufferBattleTowerElevatorFloors)
	waitse
	special(MoveElevator)
	waitstate
	delay(48)
	applymovement(ATTENDANT, BattleFrontier_BattleTowerElevator_Movement_AttendantExit)
	applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleTowerElevator_Movement_PlayerExit)
	waitmovement(0)
	call(BattleFrontier_BattleTowerElevator_EventScript_WarpToNextRoom)
	end
}

script BattleFrontier_BattleTowerElevator_EventScript_WarpToNextRoom{
    switch(var(VAR_FRONTIER_BATTLE_MODE)){
        case FRONTIER_MODE_SINGLES: 
        case FRONTIER_MODE_DOUBLES: call(BattleFrontier_BattleTowerElevator_EventScript_WarpToCorridor)
        case FRONTIER_MODE_MULTIS: call(BattleFrontier_BattleTowerElevator_EventScript_WarpToNextRoomMulti)
        case FRONTIER_MODE_LINK_MULTIS: call(BattleFrontier_BattleTowerElevator_EventScript_WarpToCorridorMulti)
    }
	return
}

script BattleFrontier_BattleTowerElevator_EventScript_WarpToCorridor{
	warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_CORRIDOR, 8, 1)
	waitstate
	return
}

script BattleFrontier_BattleTowerElevator_EventScript_WarpToNextRoomMulti{
    if (!flag(FLAG_CHOSEN_MULTI_BATTLE_NPC_PARTNER)){
        goto(BattleFrontier_BattleTowerElevator_EventScript_WarpToPartnerRoom)
    }
    
    call(BattleFrontier_BattleTowerElevator_EventScript_WarpToCorridorMulti)
}

script BattleFrontier_BattleTowerElevator_EventScript_WarpToCorridorMulti{
	warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_MULTI_CORRIDOR, 7, 2)
	waitstate
	return
}

script BattleFrontier_BattleTowerElevator_EventScript_WarpToPartnerRoom{
	warp(MAP_BATTLE_FRONTIER_BATTLE_TOWER_MULTI_PARTNER_ROOM, 10, 1)
	waitstate
	return
}

movement BattleFrontier_BattleTowerElevator_Movement_AttendantEnter{
	walk_up
	walk_right
	face_down
	step_end
}

movement BattleFrontier_BattleTowerElevator_Movement_PlayerEnter{
	walk_up * 2
	face_down
	step_end
}

movement BattleFrontier_BattleTowerElevator_Movement_AttendantExit{
	walk_down * 2
	set_invisible
	step_end
}

movement BattleFrontier_BattleTowerElevator_Movement_PlayerExit{
	walk_right
	walk_down * 2
	step_end
}

