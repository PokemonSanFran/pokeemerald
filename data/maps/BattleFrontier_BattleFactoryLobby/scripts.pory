//written by Jaizu
//factory lobby
//notes: it has some callnatives for merrp followers and some textcolor bcs i used fr text colors
//but the compiler should just complain about those and u can freely remove them
//I dont have the text colors in every place but I already had some implemented before starting converting it to .pory
//so it is what it is lol
//it also removes 2 labels needed by some battle tent
//not sure if u need those, should be easy to copy paste back from emerald
//![image](https://github.com/PokemonSanFran/pokeemerald/assets/77138753/308d57e1-91ba-4e9e-bccd-a11fe3d43a96)


const LOCALID_ATTENDANT_SINGLES = 1
const LOCALID_ATTENDANT_DOUBLES = 6

mapscripts BattleFrontier_BattleFactoryLobby_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_TEMP_CHALLENGE_STATUS, 0 {
			frontier_getstatus
			end
		}
		VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_SAVING {
			lockall
			textcolor(NPC_TEXT_COLOR_MALE)
			msgbox(format("I'm sorry to say this, but you didn't save before you quit playing last time.\p"
							"As a result, you have been disqualified from your challenge."), MSGBOX_DEFAULT)
			closemessage
			factory_setswapped
			factory_set(FACTORY_DATA_WIN_STREAK_SWAPS, 0)
			factory_set(FACTORY_DATA_WIN_STREAK, 0)
			factory_set(FACTORY_DATA_WIN_STREAK_ACTIVE, FALSE)
			frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, 0)
			setvar(VAR_TEMP_CHALLENGE_STATUS, 255)
			releaseall
			end
		}
		VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_PAUSED {
			lockall
			textcolor(NPC_TEXT_COLOR_MALE)
			message(format("We've been waiting for you!\p"
							"Before we resume your challenge, I must save the game."))
			waitmessage
			factory_save(CHALLENGE_STATUS_SAVING)
			playse(SE_SAVE)
			waitse
			frontier_set(FRONTIER_DATA_PAUSED, FALSE)
			setvar(VAR_TEMP_CHALLENGE_STATUS, 255)
			setvar(VAR_0x8006, 2)
			goto(BattleFrontier_BattleFactoryLobby_EventScript_EnterChallenge)
		}
		VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_WON {
			lockall
			frontier_isbrain
			if (var(VAR_RESULT) == TRUE)
			{
				msgbox(format("Congratulations for defeating the Factory Head and winning seven matches in a row!"), MSGBOX_DEFAULT)
			}
			else
			{
				msgbox(format("Congratulations! You've won seven straight Battle Swap matches!"), MSGBOX_DEFAULT)
			}
			waitmessage
			msgbox(format("In recognition of your wealth of knowledge and keen intelligence, we award you these Battle Point(s)."), MSGBOX_DEFAULT)
			frontier_givepoints
			msgbox(BattleFrontier_Text_ObtainedXBattlePoints, MSGBOX_GETPOINTS)
			message(format("Let me exchange your Pokémon for our rental Pokémon.\p"
					"I need to save the battle data, so please wait."))
			waitmessage
			frontier_checkairshow
			special(LoadPlayerParty)
			callnative(UpdateFollowingPokemon)
			factory_save(0)
			playse(SE_SAVE)
			waitse
			goto(BattleFrontier_BattleFactoryLobby_EventScript_AskRecordBattle)
			end
		}
		VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_LOST {
			lockall
			message(format("Thank you for participating!\p"
							"I will return your Pokémon in exchange for our rental Pokémon.\p"
							"I must also save the event results.\n"
							"Please wait."))
			waitmessage
			frontier_checkairshow
			special(LoadPlayerParty)
			callnative(UpdateFollowingPokemon)
			factory_set(FACTORY_DATA_WIN_STREAK_ACTIVE, FALSE)
			factory_save(0)
			playse(SE_SAVE)
			waitse
			goto(BattleFrontier_BattleFactoryLobby_EventScript_AskRecordBattle)
		}
	]
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_TEMP_1, 0 {
			setvar(VAR_TEMP_1, 1)
			turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
			end
		}
	]
}

script BattleFrontier_BattleFactoryLobby_EventScript_AskRecordBattle {
	call(BattleFrontier_EventScript_GetCantRecordBattle)
	if(var(VAR_RESULT) == TRUE)
	{
		goto(BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle)
	}
	message(format("Would you like to record your last Battle Factory match on your Frontier Pass?"))
	waitmessage
	multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
	switch(var(VAR_RESULT))
	{
		case 1:
			call(BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle)
		case 0:
			call(BattleFrontier_EventScript_SaveBattle)
			goto(BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle)
		case MULTI_B_PRESSED:
			call(BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle)
	}
}

script BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle {
	msgbox("We look forward to your next visit.", MSGBOX_DEFAULT)
	closemessage
	setvar(VAR_TEMP_CHALLENGE_STATUS, 255)
	releaseall
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_SinglesAttendant {
	lock
	faceplayer
	setvar(VAR_FRONTIER_FACILITY, FRONTIER_FACILITY_FACTORY)
	setvar(VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_SINGLES)
	goto(BattleFrontier_BattleFactoryLobby_EventScript_Attendant)
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_DoublesAttendant {
	setvar(VAR_FRONTIER_FACILITY, FRONTIER_FACILITY_FACTORY)
	setvar(VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_DOUBLES)
	goto(BattleFrontier_BattleFactoryLobby_EventScript_Attendant)
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_Attendant {
	special(SavePlayerParty)
	if(var(VAR_FRONTIER_BATTLE_MODE) == FRONTIER_MODE_SINGLES)
	{
		msgbox(format("Where the intelligence of Trainers\n"
					"is put to the test!\p"
					"Welcome to the Battle Factory!\p"
					"I am your guide to the Battle Swap Single Tournament."), MSGBOX_DEFAULT)
	}
	else
	{
		msgbox(format("Where the intelligence of Trainers is put to the test!\p"
						"Welcome to the Battle Factory!\p"
						"I am your guide to the Battle Swap Double Tournament."), MSGBOX_DEFAULT)
	}
	goto(BattleFrontier_BattleFactoryLobby_EventScript_AskTakeChallenge)
}

script BattleFrontier_BattleFactoryLobby_EventScript_AskTakeChallenge {
	if(var(VAR_FRONTIER_BATTLE_MODE) == FRONTIER_MODE_SINGLES)
	{
		message(format("Would you like to take the Battle Swap Single challenge?"))
	}
	else
	{
		message(format("Would you like to take the Battle Swap Double challenge?"))
	}
	waitmessage
	multichoice(17, 6, MULTI_CHALLENGEINFO, FALSE)
	switch(var(VAR_RESULT))
	{
		case 0:
			message(format("Which level do you wish to challenge? Level 50 or Open Level?"))
			waitmessage
			multichoice(17, 6, MULTI_LEVEL_MODE, FALSE)
			switch(var(VAR_RESULT))
			{
				case FRONTIER_LVL_TENT:
					call(BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge)
				case MULTI_B_PRESSED:
					call(BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge)
			}
			frontier_set(FRONTIER_DATA_LVL_MODE, VAR_RESULT)
			msgbox(format("Before you begin your challenge,\n"
							"I need to save the game. Is that okay?"), MSGBOX_YESNO)
			switch(var(VAR_RESULT))
			{
				case YES:
					setvar (VAR_TEMP_CHALLENGE_STATUS, 0)
					factory_init
					frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_SAVING)
					factory_set(FACTORY_DATA_WIN_STREAK_ACTIVE, TRUE)
					frontier_set(FRONTIER_DATA_PAUSED, FALSE)
					closemessage
					delay(2)
					call(Common_EventScript_SaveGame)
					setvar(VAR_TEMP_CHALLENGE_STATUS, 255)
					if(var(VAR_RESULT) == 0)
					{
						frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, 0)
						goto(BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge)
					}
					setvar(VAR_0x8006, 0)
					goto(BattleFrontier_BattleFactoryLobby_EventScript_EnterChallenge)
				case NO:
				case MULTI_B_PRESSED:
					special(LoadPlayerParty)
					callnative(UpdateFollowingPokemon)
					goto(BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge)
			}
		case 1:
			if(var(VAR_FRONTIER_BATTLE_MODE) == FRONTIER_MODE_SINGLES)
			{
				msgbox(format("The Battle Swap Single Tournament is a Single Battle competition using only rental Pokémon.\p"
							"First, you will be loaned three Pokémon specifically for this event.\p"
							"Using the supplied rental Pokémon, you must conduct a Single Battle.\p"
							"If you win, you are permitted to trade one of your rental Pokémon.\p"
							"Repeat this cycle of battling and trading--if you win seven times in a row, you will earn Battle Points.\p"
							"If you want to interrupt your challenge, please save the game.\p"
							"If you don't save, you will be disqualified from your challenge."), MSGBOX_DEFAULT)
			}
			else
			{
				msgbox(	format("The Battle Swap Double Tournament is a Double Battle competition using only rental Pokémon.\p"
								"First, you will be loaned three Pokémon specifically for this event.\p"
								"Using the supplied rental Pokémon, you must conduct a Double Battle.\p"
								"If you win, you are permitted to trade one of your rental Pokémon.\p"
								"Repeat this cycle of battling and trading--if you win seven times in a row, you will earn Battle Points.\p"
								"If you want to interrupt your challenge, please save the game.\p"
								"If you don't save before interrupting, you will be disqualified."), MSGBOX_DEFAULT)
			}
			goto(BattleFrontier_BattleFactoryLobby_EventScript_AskTakeChallenge)
		case 2:
		case MULTI_B_PRESSED:
			call(BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge)
	}
}

script BattleFrontier_BattleFactoryLobby_EventScript_EnterChallenge {
	special(SavePlayerParty)
	msgbox("Please step this way.", MSGBOX_DEFAULT)
	closemessage
	if(var(VAR_FRONTIER_BATTLE_MODE) == FRONTIER_MODE_SINGLES)
	{
		setvar(VAR_LAST_TALKED, LOCALID_ATTENDANT_SINGLES)
	}
	else
	{
		setvar(VAR_LAST_TALKED, LOCALID_ATTENDANT_DOUBLES)
	}
	applymovement(VAR_LAST_TALKED, BattleFrontier_BattleFactoryLobby_Movement_AttendantEnterDoor)
	applymovement(OBJ_EVENT_ID_PLAYER, BattleFrontier_BattleFactoryLobby_Movement_PlayerEnterDoor)
	waitmovement(0)
	warp(MAP_BATTLE_FRONTIER_BATTLE_FACTORY_PRE_BATTLE_ROOM, 8, 13)
	setvar(VAR_TEMP_CHALLENGE_STATUS, 0)
	waitstate
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge {
	msgbox("We look forward to your next visit.", MSGBOX_DEFAULT)
	release
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_ShowSinglesResults {
	lockall
	frontier_results(FRONTIER_FACILITY_FACTORY, FRONTIER_MODE_SINGLES)
	waitbuttonpress
	special(RemoveRecordsWindow)
	releaseall
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_ShowDoublesResults {
	lockall
	frontier_results(FRONTIER_FACILITY_FACTORY, FRONTIER_MODE_DOUBLES)
	waitbuttonpress
	special(RemoveRecordsWindow)
	releaseall
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_Woman {
	msgbox(format("Hi!\n"
			"You, there!\p"
			"Are you thinking that the events here are easy since you don't need to have a raised team of Pokémon?\p"
			"I wouldn't be too sure about winning that easily.\p"
			"If you don't have thorough knowledge about Pokémon and their moves, it will be tough to keep winning."), MSGBOX_NPC)
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_Camper {
	msgbox(format("I swapped for a weak Pokémon I thought it was a good kind to have…\p"
			"They wiped the floor with us…"), MSGBOX_NPC)
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_Picnicker {
	lock
	msgbox(format("Things haven't been going my way at all.\p"
			"You need to check your opponent's Pokémon during battle to see if they're any good."), MSGBOX_DEFAULT)
	release
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_FatMan {
	lock
	msgbox(format("You know how the staff here give you a few hints about your next opponent?\p"
			"Well, I'm a full-grown man, but I have trouble figuring out their hints."), MSGBOX_NPC)
	release
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_RulesBoard {
	lockall
	msgbox("The Battle Swap rules are listed.", MSGBOX_DEFAULT)
	goto(BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard)
	end
}

script BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard {
	message(format("Which heading do you want to read?"))
	waitmessage
	multichoice(17, 0, MULTI_BATTLE_FACTORY_RULES, FALSE)
	if (var(VAR_RESULT) == 5 || var(VAR_RESULT) == MULTI_B_PRESSED)
	{
		releaseall
		end
	}
	switch(var(VAR_RESULT))
	{
		case 0:
			msgbox(format("In a Battle Swap event, you may use only three Pokémon.\p"
						"Whether you are renting or swapping, your team may not have two or more of the same Pokémon."), MSGBOX_DEFAULT)
		case 1:
			msgbox(format("You may swap Pokémon only with the Trainer you have just defeated.\p"
						"You may swap for only those Pokémon used by the beaten Trainer."), MSGBOX_DEFAULT)
		case 2:
			msgbox(format("After every battle you win, you may swap for one of your defeated opponent's Pokémon.\p"
						"You will not be able to swap Pokémon with the seventh Trainer in the event."), MSGBOX_DEFAULT)
		case 3:
			msgbox(format("There are two key points to be aware of when swapping Pokémon.\p"
							"First, when swapping, you can't check the stats of the Pokémon you are about to receive.\p"
							"Second, the Pokémon on your team are lined up in sequence, depending on the order in which you rented them.\p"
							"This sequence remains unchanged even when swaps are made."), MSGBOX_DEFAULT)
		case 4:
			msgbox(format("In the Open Level, the rental Pokémon and the opposing Trainers' Pokémon are all Level 100."), MSGBOX_DEFAULT)
	}
	goto(BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard)
}

movement BattleFrontier_BattleFactoryLobby_Movement_AttendantEnterDoor {
	walk_up * 3
	set_invisible
}

movement BattleFrontier_BattleFactoryLobby_Movement_PlayerEnterDoor {
	walk_up * 4
}
