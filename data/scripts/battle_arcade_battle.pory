const VAR_BRAIN_STATUS = VAR_TEMP_F
const VAR_HEARD_BRAIN_INTRO = VAR_TEMP_2

const GAME_BOARD_EVENT =  VAR_0x8007
const GAME_BOARD_IMPACT = VAR_0x8008
const GAME_BOARD_SUCCESS = VAR_0x8009

script BattleFrontier_RouletteRoom_EventScript_BeginChallenge
{
    setvar(VAR_TEMP_0, 2)
    call(BattleArcade_BattleRoom_EventScript_MCFaceCrowd)
    msgbox(BattleArcade_RouletteRoom_Text_WelcomeToTheBattleArcade,MSGBOX_DEFAULT)
    msgbox(BattleArcade_RouletteRoom_Text_FirstIllNeedToHoldOn,MSGBOX_DEFAULT)
    goto(BattleArcade_RouletteRoom_EventScript_SpawnOpponentStartGameBoard)
}

script BattleArcade_RouletteRoom_EventScript_BufferNextBattleNumber
{
    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    addvar(VAR_RESULT,1)
    buffernumberstring(STR_VAR_1,VAR_RESULT)
}

script BattleArcade_RouletteRoom_EventScript_AnnounceGameNumber
{
    call(BattleArcade_RouletteRoom_EventScript_BufferNextBattleNumber)
    msgbox(BattleArcade_RouletteRoom_Text_GameNoComeOn,MSGBOX_DEFAULT)
    closemessage
}

script BattleArcade_RouletteRoom_EventScript_PlayGameBoard
{
    additem(ITEM_COIN_CASE,1)
	addcoins(50)
    msgbox(BattleArcade_RouletteRoom_Text_WithoutFurtherAdo,MSGBOX_DEFAULT)
    closemessage
	specialvar(VAR_RESULT, GetSlotMachineId)
	playslotmachine(VAR_RESULT)
}

script BattleArcade_RouletteRoom_EventScript_HandleGameResults
{
    arcade_generateopponentparty
    arcade_takeplayerhelditems
    arcade_takeenemyhelditems

    arcade_playgameboard
    arcade_handlegameresult
    arcade_getimpactedside
    arcade_getevent

    msgbox(BattleArcade_RouletteRoom_Text_ThisIsOurCurrentEvent,MSGBOX_DEFAULT)
    call(BattleArcade_BattleRoom_EventScript_ApplyOverworldImpact)
    call(BattleArcade_RouletteRoom_AnnounceImpactString)
    call(BattleArcade_BattleRoom_EventScript_MoveMCBack)
    call(BattleArcade_RouletteRoom_SkipBattle_Script)
}

script BattleArcade_RouletteRoom_AnnounceImpactString
{
    if (var(GAME_BOARD_SUCCESS))
    {
        call(BattleArcade_RouletteRoom_AnnounceImpactString_Success)
    }
    else
    {
        call(BattleArcade_RouletteRoom_AnnounceImpactString_Failure)
    }
}

script BattleArcade_RouletteRoom_SkipBattle_Script
{
    if (var(GAME_BOARD_EVENT) != ARCADE_EVENT_NO_BATTLE)
    {
        return
    }

    call(BattleArcade_BattleRoom_EventScript_MoveBattlersIntoPosition)
    goto(BattleArcade_RouletteRoom_EventScript_DefeatedOpponent)
}

script BattleArcade_RouletteRoom_AnnounceImpactString_Success
{
    switch(var(GAME_BOARD_EVENT))
    {
        case ARCADE_EVENT_LOWER_HP:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommenceWithoutFullHp,MSGBOX_DEFAULT)
        case ARCADE_EVENT_POISON:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommencePoisoned,MSGBOX_DEFAULT)
        case ARCADE_EVENT_PARALYZE:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommenceParalyzed,MSGBOX_DEFAULT)
        case ARCADE_EVENT_BURN:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommenceBurned,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SLEEP:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommenceAsleep,MSGBOX_DEFAULT)
        case ARCADE_EVENT_FREEZE:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommenceFrozen,MSGBOX_DEFAULT)
        case ARCADE_EVENT_GIVE_BERRY:
        case ARCADE_EVENT_GIVE_ITEM:
            msgbox(BattleArcade_RouletteRoom_Text_PokemonWillBeLoanedThe,MSGBOX_DEFAULT)
        case ARCADE_EVENT_LEVEL_UP:
            msgbox(BattleArcade_RouletteRoom_Text_MatchCommenceLeveledUp,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SUN:
            msgbox(BattleArcade_RouletteRoom_Text_MatchConductedHarshSunlight,MSGBOX_DEFAULT)
        case ARCADE_EVENT_RAIN:
            msgbox(BattleArcade_RouletteRoom_Text_MatchConductedSteadyRainfall,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SAND:
            msgbox(BattleArcade_RouletteRoom_Text_MatchConductedRagingSandstorm,MSGBOX_DEFAULT)
        case ARCADE_EVENT_HAIL:
            msgbox(BattleArcade_RouletteRoom_Text_MatchConductedPeltingHail,MSGBOX_DEFAULT)
        case ARCADE_EVENT_FOG:
            msgbox(BattleArcade_RouletteRoom_Text_MatchConductedDenseFog,MSGBOX_DEFAULT)
        case ARCADE_EVENT_TRICK_ROOM:
            msgbox(BattleArcade_RouletteRoom_Text_MatchConductedTimeAndSpaceAreDistorted,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SWAP:
            msgbox(BattleArcade_RouletteRoom_Text_YourPokemonWillBeSwitched,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SPEED_UP:
            msgbox(BattleArcade_RouletteRoom_Text_BoardspeedIncreased,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SPEED_DOWN:
            msgbox(BattleArcade_RouletteRoom_Text_BoardspeedDecreased,MSGBOX_DEFAULT)
        case ARCADE_EVENT_RANDOM:
            msgbox(BattleArcade_RouletteRoom_Text_BoardmotionRandom,MSGBOX_DEFAULT)
        case ARCADE_EVENT_GIVE_BP_SMALL:
        case ARCADE_EVENT_GIVE_BP_BIG:
            msgbox(BattleArcade_RouletteRoom_Text_ReceivedBp,MSGBOX_DEFAULT)
        case ARCADE_EVENT_NO_BATTLE:
            msgbox(BattleArcade_RouletteRoom_Text_OhLuckyYouCanSkip,MSGBOX_DEFAULT)
        default:
        case ARCADE_EVENT_NO_EVENT:
            msgbox(BattleArcade_RouletteRoom_Text_TooBadThereIsNo,MSGBOX_DEFAULT)
    }
}

script BattleArcade_RouletteRoom_AnnounceImpactString_Failure
{
    switch(var(GAME_BOARD_EVENT))
    {
        case ARCADE_EVENT_POISON:
            msgbox(BattleArcade_RouletteRoom_Text_MatchBurnedLuckyBreak,MSGBOX_DEFAULT)
        case ARCADE_EVENT_PARALYZE:
            msgbox(BattleArcade_RouletteRoom_Text_MatchParalyzedLuckyBreak,MSGBOX_DEFAULT)
        case ARCADE_EVENT_BURN:
            msgbox(BattleArcade_RouletteRoom_Text_MatchBurnedLuckyBreak,MSGBOX_DEFAULT)
        case ARCADE_EVENT_SLEEP:
            msgbox(BattleArcade_RouletteRoom_Text_MatchAsleepLuckyBreak,MSGBOX_DEFAULT)
        default:
        case ARCADE_EVENT_FREEZE:
            msgbox(BattleArcade_RouletteRoom_Text_MatchFrozenLuckyBreak,MSGBOX_DEFAULT)
    }
}

script BattleArcade_RouletteRoom_EventScript_SpawnOpponentStartGameBoard
{
    frontier_set(FRONTIER_DATA_LVL_MODE, 1) // Debug
    frontier_set(FRONTIER_MODE_SINGLES,1) // Debug
    tower_setopponent
    call(BattleArcade_RouletteRoom_EventScript_AnnounceGameNumber)
    call(BattleArcade_BattleRoom_EventScript_SpawnOpponentEntrance)
    call(BattleArcade_RouletteRoom_EventScript_StartGameBoard)
    call(BattleArcade_BattleRoom_EventScript_OpponentBattleMovement)
    call(BattleArcade_RouletteRoom_EventScript_LoadOpponentText)
    call(BattleArcade_RouletteRoom_EventScript_StartBattle)
    goto(BattleArcade_RouletteRoom_EventScript_HandleBattleResult)
}

script BattleArcade_RouletteRoom_EventScript_StartGameBoard
{
    call(BattleArcade_BattleRoom_EventScript_MCMoveIntoPosition)
    call(BattleArcade_RouletteRoom_EventScript_PlayGameBoard)
    call(BattleArcade_RouletteRoom_EventScript_HandleGameResults)
}

script BattleArcade_RouletteRoom_EventScript_LoadOpponentText
{
    tower_getopponentintro(0)
    msgbox(gStringVar4, MSGBOX_DEFAULT)
    waitmessage
    closemessage
}

script BattleArcade_RouletteRoom_EventScript_StartBattle
{
    call(BattleArcade_BattleRoom_EventScript_MoveBattlersIntoPosition)
    call(BattleArcade_RouletteRoom_EventScript_DoRouletteBattle)
}

script BattleArcade_RouletteRoom_EventScript_DoRouletteBattle
{
    closemessage

    setvar(VAR_HEARD_BRAIN_INTRO, 0)
    frontier_set(FRONTIER_DATA_RECORD_DISABLED, FALSE)
    setvar(VAR_0x8004, SPECIAL_BATTLE_TOWER)
    setvar(VAR_0x8005, 0)

    special(DoSpecialRouletteTrainerBattle)
    waitstate

    copyvar(VAR_0x8004, VAR_FRONTIER_BATTLE_MODE)
    if(var(VAR_0x8004) != FRONTIER_MODE_LINK_MULTIS)
    {
        frontier_resetsketch
    }

    arcade_eventcleanup
    frontier_get(FRONTIER_DATA_BATTLE_OUTCOME)
    return
}

script BattleArcade_RouletteRoom_EventScript_WarpToLobbyLost
{
    frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_LOST)
    goto(BattleArcade_RouletteRoom_EventScript_SetLevelModeAndWarp)
}

script BattleArcade_RouletteRoom_EventScript_WarpToLobbyWon
{
    frontier_set(FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_WON)
    goto(BattleArcade_RouletteRoom_EventScript_SetLevelModeAndWarp)
}

script BattleArcade_RouletteRoom_EventScript_DefeatedOpponent
{
    call(BattleFrontier_EventScript_IncrementWinStreak)
    arcade_setbattlewon
    if (var(VAR_RESULT) == FRONTIER_STAGES_PER_CHALLENGE)
    {
        goto(BattleArcade_RoulettRoom_EventScript_ResolveEndChallenge)
    }
    call(BattleArcade_BattleRoom_EventScript_MoveBattlersBack)
    call(BattleArcade_BattleRoom_EventScript_HandleHealAttendantMovement)
    goto(BattleArcade_RouletteRoom_EventScript_HealPlayer)
}

script BattleArcade_RouletteRoom_EventScript_HealPlayer
{
    msgbox(BattleArcade_RouletteRoom_Text_GoodGoingWellHeal)
    playfanfare(MUS_HEAL)
    waitfanfare
    special(HealPlayerParty)
    closemessage
    call(BattleArcade_BattleRoom_EventScript_AttendantLeave)
    goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
}

script BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent
{
    arcade_getbrainstatus
    if(var(VAR_BRAIN_STATUS) != FRONTIER_BRAIN_NOT_READY)
    {
        goto(BattleArcade_RouletteRoom_EventScript_BrainUpNext)
    }

    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    call(BattleArcade_RouletteRoom_EventScript_ReadyForOpponent)
    call(BattleFrontier_EventScript_GetCantRecordBattle)
    if(var(VAR_RESULT) == TRUE)
    {
        goto(BattleArcade_RouletteRoom_EventScript_ReadyMenuNoRecord)
    }
    goto(BattleArcade_RouletteRoom_EventScript_ReadyMenu)

}

script BattleArcade_RouletteRoom_EventScript_ReadyMenu
{
    multichoice(19, 4, MULTI_GO_ON_RECORD_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT))
    {
        case 0:goto(BattleArcade_RouletteRoom_EventScript_ContinueChallenge)
        case 1:goto(BattleArcade_RouletteRoom_EventScript_AskRecordBattleNextBattle)
        case 2:goto(BattleArcade_RouletteRoom_EventScript_AskPauseChallenge)
        case 3:goto(BattleArcade_RouletteRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
    }
}

script BattleArcade_RouletteRoom_EventScript_ReadyMenuNoRecord
{
    multichoice(20, 6, MULTI_GO_ON_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT)){
        case 0:goto(BattleArcade_RouletteRoom_EventScript_ContinueChallenge)
        case 1:goto(BattleArcade_RouletteRoom_EventScript_AskPauseChallenge)
        case 2:goto(BattleArcade_RouletteRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
    }
}

script BattleArcade_RouletteRoom_EventScript_AskRecordBattle
{
    message(BattleFrontier_BattleTowerBattleRoom_Text_RecordYourBattle)
    waitmessage
    multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
    if(var(VAR_RESULT) == 0)
    {
        call(BattleFrontier_EventScript_SaveBattle)
    }
}

script BattleArcade_RouletteRoom_EventScript_AskRecordBattleNextBattle
{
    call(BattleArcade_RouletteRoom_EventScript_AskRecordBattle)
    goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
}

script BattleArcade_RouletteRoom_EventScript_ReadyForOpponent
{
    call(BattleArcade_RouletteRoom_EventScript_BufferNextBattleNumber)
    message(BattleArcade_RouletteRoom_Text_NextUpGameNoAreYouReady)
    waitmessage
    return
}

script BattleArcade_RouletteRoom_EventScript_AskPauseChallenge
{
    msgbox(BattleArcade_RouletteRoom_Text_WouldYouLikeToSaveAndTakeABreak, MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES)
{
    goto(BattleArcade_RouletteRoom_EventScript_PauseChallenge)
}
    goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
}

script BattleArcade_RouletteRoom_EventScript_PauseChallenge
{
    message(BattleArcade_RouletteRoom_Text_SavingDontTurnOffThePower)
    waitmessage

    tower_save(CHALLENGE_STATUS_PAUSED)
    playse(SE_SAVE)
    waitse

    fadescreen(FADE_TO_BLACK)
    frontier_reset
    end
}

script BattleArcade_RouletteRoom_EventScript_AskRetireChallenge
{
    message(BattleArcade_RouletteRoom_Text_WouldYouLikeToRetire)
    waitmessage
    multichoicedefault(20, 8, MULTI_YESNO, 1, FALSE)
    if(var(VAR_RESULT) == 0)
    {
        goto(BattleArcade_RouletteRoom_EventScript_RetireChallenge)
    }
    goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
}

script BattleArcade_RouletteRoom_EventScript_RetireChallenge
{
    setflag(FLAG_CANCEL_BATTLE_ROOM_CHALLENGE)
    goto(BattleArcade_RouletteRoom_EventScript_WarpToLobbyLost)
}

script BattleArcade_RouletteRoom_EventScript_ContinueChallenge
{
    closemessage
    goto(BattleArcade_RouletteRoom_EventScript_SpawnOpponentStartGameBoard)
}

script BattleArcade_RouletteRoom_EventScript_SetLevelModeAndWarp
{
    arcade_set(ARCADE_DATA_LVL_MODE)
    goto(BattleArcade_RouletteRoom_EventScript_WarpToLobby)
}

script BattleArcade_RouletteRoom_EventScript_WarpToLobby
{
    copyvar(VAR_RESULT,VAR_FRONTIER_BATTLE_MODE)
    if (var(VAR_RESULT) == FRONTIER_MODE_MULTIS)
    {
        warpsilent(MAP_BATTLE_ARCADE_LOBBY,6,6)
    }
    else
    {
        warpsilent(MAP_BATTLE_ARCADE_LOBBY,5,6)
    }
    waitstate
    end
}

script BattleArcade_RouletteRoom_EventScript_BrainUpNext
{
    if (var(VAR_HEARD_BRAIN_INTRO) == 1)
    {
        goto(BattleArcade_RouletteRoom_EventScript_AskReadyForBrain)
    }

    msgbox(BattleArcade_RouletteRoom_Text_CongratulationsTrainerInRecognition, MSGBOX_DEFAULT)
    setvar(VAR_HEARD_BRAIN_INTRO,1)

    goto(BattleArcade_RouletteRoom_EventScript_AskReadyForBrain)
}

script BattleArcade_RouletteRoom_EventScript_AskReadyForBrain
{
    call(BattleFrontier_EventScript_GetCantRecordBattle)
    if(var(VAR_RESULT) == TRUE)
    {
        goto(BattleArcade_RouletteRoom_EventScript_BrainReadyMenu)
    }
    else
    {
        goto(BattleArcade_RouletteRoom_EventScript_BrainReadyMenuNoRecord)
    }
}

script BattleArcade_RouletteRoom_EventScript_BrainReadyMenu
{
    multichoice(19, 4, MULTI_GO_ON_RECORD_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT))
    {
        case 0:goto(BattleArcade_RouletteRoom_EventScript_BattleBrain)
        case 1:goto(BattleArcade_RouletteRoom_EventScript_AskRecordBattleNextBattle)
        case 2:goto(BattleArcade_RouletteRoom_EventScript_AskPauseChallenge)
        case 3:goto(BattleArcade_RouletteRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
    }
}

script BattleArcade_RouletteRoom_EventScript_BrainReadyMenuNoRecord
{
    multichoice(20, 6, MULTI_GO_ON_REST_RETIRE, TRUE)
    switch(var(VAR_RESULT))
    {
        case 0:goto(BattleArcade_RouletteRoom_EventScript_BattleBrain)
        case 1:goto(BattleArcade_RouletteRoom_EventScript_AskPauseChallenge)
        case 2:goto(BattleArcade_RouletteRoom_EventScript_AskRetireChallenge)
        case MULTI_B_PRESSED:goto(BattleArcade_RouletteRoom_EventScript_AskReadyForOpponent)
    }
}

script BattleArcade_RouletteRoom_EventScript_LoadBrainText
{
    bufferstring(STR_VAR_3,"hello")
    arcade_getbrainintro
    msgbox(gStringVar4, MSGBOX_DEFAULT)
    waitmessage
    closemessage
}

script BattleArcade_RouletteRoom_EventScript_BattleBrain
{
    closemessage
    call(BattleFrontier_EventScript_SetBrainObjectGfx)
    call(BattleArcade_RouletteRoom_EventScript_BrainIntro)
    call(BattleArcade_RouletteRoom_EventScript_StartGameBoard)
    call(BattleArcade_BattleRoom_EventScript_OpponentBattleMovement)
    call(BattleArcade_RouletteRoom_EventScript_LoadBrainText)
    call(BattleArcade_RouletteRoom_EventScript_StartBattle)
    goto(BattleArcade_RouletteRoom_EventScript_HandleBrainBattleResult)
    //goto(BattleArcade_RouletteRoom_EventScript_HandleBattleResult)
}

script BattleArcade_RouletteRoom_EventScript_HandleBattleResult
{
    if (var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(BattleArcade_RouletteRoom_EventScript_DefeatedOpponent)
    }
    goto(BattleArcade_RouletteRoom_EventScript_WarpToLobbyLost)
}

script BattleArcade_RouletteRoom_EventScript_HandleBrainBattleResult
{
    if(var(VAR_RESULT) == B_OUTCOME_WON)
    {
        goto(BattleArcade_RouletteRoom_EventScript_DefeatedBrain)
    }
    goto(BattleFrontier_BattleTower_EventScript_WarpToLobbyLost)
}

script BattleArcade_RouletteRoom_EventScript_DefeatedBrain
{
    call(BattleFrontier_EventScript_IncrementWinStreak)
    call(BattleArcade_RouletteRoom_EventScript_BrainCongratsText)
    call(BattleArcade_BattleRoom_EventScript_MoveBattlersBack)
    goto(BattleArcade_RoulettRoom_EventScript_ResolveEndChallenge)
}

script BattleArcade_RoulettRoom_EventScript_ResolveEndChallenge
{
    call(BattleFrontier_EventScript_GetCantRecordBattle)
    if(var(VAR_RESULT) == FALSE)
    {
        call(BattleArcade_RouletteRoom_EventScript_AskRecordBattle)
    }
    call(BattleArcade_RouletteRoom_EventScript_GiveBP)
    call(BattleArcade_RouletteRoom_EventScript_RestoreItems)
    call(BattleArcade_RouletteRoom_EventScript_Save)
    goto(BattleArcade_RouletteRoom_EventScript_WarpToLobbyWon)
}

script BattleArcade_RouletteRoom_EventScript_Save
{
    msgbox(gText_SavingDontTurnOff2)
    tower_save(CHALLENGE_STATUS_SAVING)
    waitstate
    playse(SE_SAVE)
    waitse
    closemessage
}

script BattleArcade_RouletteRoom_EventScript_GiveBP
{
    arcade_givepoints
    msgbox(BattleArcade_RouletteRoom_Text_YouHaveClearedYourSeventh)
}

script BattleArcade_RouletteRoom_EventScript_RestoreItems
{
    msgbox(BattleArcade_RouletteRoom_Text_WeWillReturnAnyItems)
}

script BattleArcade_RouletteRoom_EventScript_BrainCongratsText
{
    frontier_get(FRONTIER_DATA_BATTLE_NUM)
    if(var(VAR_RESULT) == 35)
    {
        msgbox(BattleArcade_RouletteRoom_Text_HowFabulousOfYouYour,MSGBOX_DEFAULT)
    }
    else
    {
        msgbox(BattleArcade_RouletteRoom_Text_TrulyItWasSoVery,MSGBOX_DEFAULT)
    }
}

script BattleArcade_RouletteRoom_EventScript_BrainIntro
{
    call(BattleArcade_BattleRoom_EventScript_BrainEntranceMovement)
    arcade_checksymbol
    msgbox(BattleArcade_RouletteRoom_Text_NoNeedToWorry,MSGBOX_DEFAULT)
    if (var(VAR_RESULT) == ARCADE_SYMBOL_GOLD)
    {
        msgbox(BattleArcade_RouletteRoom_Text_IKnowOneThing, MSGBOX_DEFAULT)
    }
    else
    {
        msgbox(BattleArcade_RouletteRoom_Text_YouAreProving,MSGBOX_DEFAULT)
    }
}

text BattleArcade_RouletteRoom_Text_WellDone
{
    format("Well done!")
}

text BattleArcade_RouletteRoom_Text_LetTheBattle
{
    format("Let the battles begin! Battle No. 1! Ready...")
}

text BattleArcade_RouletteRoom_Text_Next
{
    format("Next. Battle No. {STR_VAR_1}! Ready...")
}

text BattleArcade_RouletteRoom_Text_Go
{
    format("Go!")
}

text BattleArcade_RouletteRoom_Text_DoYouWantToContinue
{
    format("Do you want to continue arcade?")
}

text BattleArcade_RouletteRoom_Text_DoYouWantToRestore
{
    format("Do you want to restore your POKéMON? (You can restore them {STR_VAR_1} more time(s).)")
}

text BattleArcade_Entrance_MaxedOutStreak
{
    format("Wow, {PLAYER}, {STR_VAR_1} win(s) in a row... I think we all need a break!")
}

text BattleArcade_RouletteRoom_Text_FirstIllNeedToHoldOn
{
	format("First, I’ll need to hold on to any items held by your Pokémon.")
}
text BattleArcade_RouletteRoom_Text_PleaseWaitAMoment
{
	format("Please wait a moment.")
}
text BattleArcade_RouletteRoom_Text_GoodGoingWellHeal
{
	format("Good going! We’ll heal your Pokémon now.")
}
text BattleArcade_RouletteRoom_Text_NextUpGameNoAreYouReady
{
	format("Next up, game no. {STR_VAR_1}! Are you ready?")
}
text BattleArcade_RouletteRoom_Text_WouldYouLikeToSaveTheLastBattle
{
	format("Would you like to save the last battle as your Battle Video?")
}
text BattleArcade_RouletteRoom_Text_BattleHasBeenSaved
{
	format("{STR_VAR_1}’s battle has been saved as a Battle Video.")
}
text BattleArcade_RouletteRoom_Text_ThereWasAnError
{
	format("There was an error. The Battle Video couldn’t be saved.")
}
text BattleArcade_RouletteRoom_Text_DeleteYourExistingBattleVideo
{
	format("Delete your existing Battle Video and save the last battle?")
}
text BattleArcade_RouletteRoom_Text_SavingDontTurnOffThePower
{
	format("Saving... Don’t turn off the power.")
}
text BattleArcade_RouletteRoom_Text_WouldYouLikeToSaveAndTakeABreak
{
	format("Would you like to save and take a break from playing?")
}
text BattleArcade_RouletteRoom_Text_WouldYouLikeToRetire
{
	format("Would you like to retire from this challenge?")
}
text BattleArcade_RouletteRoom_Text_IsChoosingPleaseWait
{
	format("{STR_VAR_1} is choosing. Please wait.")
}
text BattleArcade_RouletteRoom_Text_ChoseToRetire
{
	format("{STR_VAR_1} chose to retire.")
}
text BattleArcade_RouletteRoom_Text_WelcomeToTheBattleArcade
{
	format("Welcome to the Battle Arcade!")
}
text BattleArcade_RouletteRoom_Text_GameNoComeOn
{
	format("Game no. {STR_VAR_1}! Come on!")
}
text BattleArcade_RouletteRoom_Text_WithoutFurtherAdo
{
	format("Without further ado... Let’s activate the game board!")
}
text BattleArcade_RouletteRoom_Text_ThisIsOurCurrentEvent
{
	format("This is our current event!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceWithoutFullHp
{
	format("The match will commence with {STR_VAR_1}’s Pokémon without full HP!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothWithoutFullHp
{
	format("The match will commence with {STR_VAR_1}’s and {STR_VAR_2}’s Pokémon without full HP!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceLeveledUp
{
	format("The match will commence with {STR_VAR_1}’s Pokémon leveled up!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothLeveledUp
{
	format("The match will commence with {STR_VAR_1}’s and {STR_VAR_2}’s Pokémon leveled up!")
}
text BattleArcade_RouletteRoom_Text_MatchCommencePoisoned
{
	format("The match will commence with {STR_VAR_1}’s Pokémon poisoned!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothPoisoned
{
	format("The match will commence with {STR_VAR_1} and {STR_VAR_2}’s Pokémon poisoned!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceParalyzed
{
	format("The match will commence with {STR_VAR_1}’s Pokémon paralyzed!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothParalyzed
{
	format("The match will commence with {STR_VAR_1} and {STR_VAR_2}’s Pokémon paralyzed!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBurned
{
	format("The match will commence with {STR_VAR_1}’s Pokémon burned!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothBurned
{
	format("The match will commence with {STR_VAR_1} and {STR_VAR_2}’s Pokémon burned!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceAsleep
{
	format("The match will commence with {STR_VAR_1}’s Pokémon asleep!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothAsleep
{
	format("The match will commence with {STR_VAR_1} and {STR_VAR_2}’s Pokémon asleep!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceFrozen
{
	format("The match will commence with {STR_VAR_1}’s Pokémon frozen!")
}
text BattleArcade_RouletteRoom_Text_MatchCommenceBothFrozen
{
	format("The match will commence with {STR_VAR_1} and {STR_VAR_2}’s Pokémon frozen!")
}
text BattleArcade_RouletteRoom_Text_PokemonWillBeLoanedThe
{
	format("{STR_VAR_1}’s Pokémon will be loaned the {STR_VAR_3}.")
}
text BattleArcade_RouletteRoom_Text_BothPokemonWillBeLoanedThe
{
	format("{STR_VAR_1} and {STR_VAR_2}’s Pokémon will be loaned the {STR_VAR_3}.")
}
text BattleArcade_RouletteRoom_Text_HereYouGo
{
	format("Here you go!")
}
text BattleArcade_RouletteRoom_Text_MatchConductedHarshSunlight
{
	format("This match will be conducted under harsh sunlight.")
}
text BattleArcade_RouletteRoom_Text_MatchConductedSteadyRainfall
{
	format("This match will be conducted under a steady rainfall.")
}
text BattleArcade_RouletteRoom_Text_MatchConductedRagingSandstorm
{
	format("This match will be conducted under a raging sandstorm.")
}
text BattleArcade_RouletteRoom_Text_MatchConductedPeltingHail
{
	format("This match will be conducted under pelting hail.")
}
text BattleArcade_RouletteRoom_Text_MatchConductedDenseFog
{
	format("This match will be conducted in a dense fog.")
}
text BattleArcade_RouletteRoom_Text_MatchConductedTimeAndSpaceAreDistorted
{
	format("This match will be conducted on a floor where time and space are distorted.")
}
text BattleArcade_RouletteRoom_Text_YourPokemonWillBeSwitched
{
	format("Your Pokémon will be switched with the other Trainer’s for this match.")
}
text BattleArcade_RouletteRoom_Text_BoardspeedIncreased
{
	format("From the next game on, the game board’s speed will be increased.")
}
text BattleArcade_RouletteRoom_Text_BoardspeedDecreased
{
	format("From the next game on, the game board’s speed will be decreased.")
}
text BattleArcade_RouletteRoom_Text_BoardmotionRandom
{
	format("For the next game, the game board’s motion will become random.")
}
text BattleArcade_RouletteRoom_Text_ReceivedBp
{
	format("{PLAYER} received {STR_VAR_3} BP!")
}
text BattleArcade_RouletteRoom_Text_AndReceivedBp
{
	format("{PLAYER} and {STR_VAR_2} received {STR_VAR_3} BP!")
}
text BattleArcade_RouletteRoom_Text_AreYouReady
{
	format("Are you ready?")
}
text BattleArcade_RouletteRoom_Text_OhLuckyYouCanSkip
{
	format("Oh! Lucky! You can skip the battle!")
}
text BattleArcade_RouletteRoom_Text_TooBadThereIsNo
{
	format("Too bad! There is no event this time!")
}
text BattleArcade_RouletteRoom_Text_OkLetTheBattleCommence
{
	format("OK! Let the battle commence!")
}
text BattleArcade_RouletteRoom_Text_MatchPoisonedLuckyBreak
{
	format("{STR_VAR_1}’s Pokémon will enter the match poisoned... Oh! A lucky break! {STR_VAR_1} has no Pokémon that can be poisoned!")
}
text BattleArcade_RouletteRoom_Text_MatchBothPoisonedLuckyBreak
{
	format("{STR_VAR_1}’s and {STR_VAR_2}’s Pokémon will begin poisoned... Oh! A lucky break! Neither of them has a Pokémon that can be poisoned!")
}
text BattleArcade_RouletteRoom_Text_MatchParalyzedLuckyBreak
{
	format("{STR_VAR_1}’s Pokémon will enter the match paralyzed... Oh! A lucky break! {STR_VAR_1} has no Pokémon that can be paralyzed!")
}
text BattleArcade_RouletteRoom_Text_MatchBothParalyzedLuckyBreak
{
	format("{STR_VAR_1}’s and {STR_VAR_2}’s Pokémon will begin paralyzed... Oh! A lucky break! Neither of them has a Pokémon that can be paralyzed!")
}
text BattleArcade_RouletteRoom_Text_MatchBurnedLuckyBreak
{
	format("{STR_VAR_1}’s Pokémon will enter the match burned... Oh! A lucky break! {STR_VAR_1} has no Pokémon that can be burned!")
}
text BattleArcade_RouletteRoom_Text_MatchBothBurnedLuckyBreak
{
	format("{STR_VAR_1}’s and {STR_VAR_2}’s Pokémon will begin burned... Oh! A lucky break! Neither of them has a Pokémon that can be burned!")
}
text BattleArcade_RouletteRoom_Text_MatchAsleepLuckyBreak
{
	format("{STR_VAR_1}’s Pokémon will enter the match asleep... Oh! A lucky break! {STR_VAR_1} has no Pokémon that can be made to sleep!")
}
text BattleArcade_RouletteRoom_Text_MatchBothAsleepLuckyBreak
{
	format("{STR_VAR_1}’s and {STR_VAR_2}’s Pokémon will begin asleep... Oh! A lucky break! Neither of them has a Pokémon that can be made to sleep!")
}
text BattleArcade_RouletteRoom_Text_MatchFrozenLuckyBreak
{
	format("{STR_VAR_1}’s Pokémon will enter the match frozen... Oh! A lucky break! {STR_VAR_1} has no Pokémon that can be frozen!")
}
text BattleArcade_RouletteRoom_Text_MatchBothFrozenLuckyBreak
{
	format("{STR_VAR_1}’s and {STR_VAR_2}’s Pokémon will begin frozen... Oh! A lucky break! Neither of them has a Pokémon that can be frozen!")
}
text BattleArcade_RouletteRoom_Text_YouHaveClearedYourSeventh
{
	format("You have cleared your seventh battle! Congratulations! You have earned BP as your prize!")
}
text BattleArcade_RouletteRoom_Text_WeWillReturnAnyItems
{
	format("We will return any items we were holding for you.")
}
text BattleArcade_RouletteRoom_Text_Continue
{
	format("CONTINUE")
}
text BattleArcade_RouletteRoom_Text_Record
{
	format("RECORD")
}
text BattleArcade_RouletteRoom_Text_Rest
{
	format("REST")
}
text BattleArcade_RouletteRoom_Text_Retire
{
	format("RETIRE")
}
text BattleArcade_RouletteRoom_Text_CongratulationsTrainerInRecognition
{
	format("Congratulations, Trainer! In recognition of your outstanding skill, our Frontier Brain is demanding a match with you. So, your next match is against the Arcade Star; no ifs or buts! Are you ready?")
}

text BattleArcade_RouletteRoom_Text_NoNeedToWorry
{
	format("Dahlia: No need to worry. {EMOJI_NOTE} Let chance do what it does. Like surprises from the game board, life goes through twists and turns. No need to worry. {EMOJI_NOTE} Things will go as they will. But, enough of that.")
}

text BattleArcade_RouletteRoom_Text_IKnowOneThing
{
	format("I know one thing for certain. You have arrived here not merely because you were lucky. Let us not waste any time. I wish to test your skills myself!")
}
text BattleArcade_RouletteRoom_Text_YouAreProving
{
	format("You are proving yourself incredible. Are you incredible because you are so lucky you shrug off bad luck entirely? Or, are you so incredibly talented to not be swayed by luck, good or bad? I wish to see for myself what brought you to me today!")
}
text BattleArcade_RouletteRoom_Text_HowFabulousOfYouYour
{
	format("Dahlia: How fabulous of you! Your love of Pokémon shone through, and, in turn, your Pokémon believed in you. That is why you handled everything thrown your way splendidly. A most wonderful victory it was!")
}
text BattleArcade_RouletteRoom_Text_TrulyItWasSoVery
{
	format("Dahlia: Truly, it was so very fabulous of all of you! Bad luck, you cast aside, and good luck, you netted. That you did so is evidence of your abilities. By defeating me, Dahlia, you have proven your mastery brilliantly! I am sincerely happy for having this battle against you!")
}
